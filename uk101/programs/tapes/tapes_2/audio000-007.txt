

 10 PRINTCHR$(12):PRINT:PRINT:PRINT" GRAPH PLOTTER"
 20 PRINT" -------------":PRINT
 30 GOSUB9000
 40 INPUT"EQUATION Y=";A$
 50 N=1000
 55 PP=53260:DD=13:NT=17
 60 IFPEEK(N)<>35ORPEEK(N+1)<>35THENN=N+1:GOTO60
 70 N=N+DD
 80 I=1:I1=1
 85 RESTORE:FORJ=1TONT:READB$,Z
 86 IFMID$(A$,I1,LEN(B$))<>B$THENNEXT:GOTO90
 87 POKEI+N,Z:I1=I1+LEN(B$)-1:GOTO95
 90 POKEI+N,ASC(MID$(A$,I1,1))
 91 IFI>48THEN97
 95 I=I+1:I1=I1+1:IFI1<=LEN(A$)THEN85
 97 FORJ=ITO50:POKEJ+N,32:NEXT
 100 REM##
 101 DEFFNX(X)=SIN(COS(X)/(X*X+1)+EXP(-X))                              
     
 110 REM FUNCTION IN
 1000 DATA+,163,-,164,*,165,/,166,^,167,=,170
 1010 DATASGN,173,INT,174,ABS,175,SQR,179
 1020 DATARND,180,LOG,181,EXP,182,COS,183
 1030 DATASIN,184,TAN,185,ATN,186
 2000 INPUT"INTEGRATION IS FROM";A
 2010 INPUT"                 TO";B
 2020 IFA>=BTHENPRINT"FIRST NO. IS LOWER LIMIT!":GOTO2000
 2030 INPUT"NO. OF STEPS";N
 2040 IFN/2<>INT(N/2)THENPRINT"NO. MUST BE EVEN!":GOTO2030
 3000 I3=FNX(A)+FNX(B):X1=(B-A)/N
 3030 X2=X1*2
 3040 I1=0:C=X1:FORI=1TON-1STEP2:I1=I1+FNX(C):C=C+X2:NEXT
 3050 I2=0:C=X2:FORI=2TON-1STEP2:I2=I2+FNX(C):C=C+X2:NEXT
 3060 I=(I3+I1*4+I2*2)*X1/3
 3070 PRINT"RESULT OF INTEGRATING F(X)=";A$
 3075 PRINT"BETWEEN";A;"AND";B;"WITH";N;"STEPS IS:";I
 3080 INPUT"NEW STEP SIZE";Q$
 3090 IFLEFT$(Q$,1)="Y"THEN2030
 3100 INPUT"NEW LIMITS";Q$
 3110 IFLEFT$(Q$,1)="Y"THEN2000
 3200 END
 8999 END
 9000 PRINT"THIS PROGRAM NUMERICALLY INTEGRATES"
 9010 PRINT"A FUNCTION OF X BETWEEN TWO LIMITS"
 9020 PRINT"USING 'SIMPSONS RULE'"
 9030 PRINT"THIS CALCULATES THE VALUE OF F(X)"
 9040 PRINT"FOR AN EVEN NO. OF EQUALLY SPACED VALUES"
 9050 PRINT"OF X BETWEEN THE LIMITS, AND ESTIMATES"
 9060 PRINT"THE INTEGRAL BY THE FOLLOWING FORMULA:"
 9065 PRINT
 9070 PRINT"I=h/3*(Y0+YN+2*(Y2+Y4+...)+4*(Y1+Y3+...))"
 9075 PRINT
 9080 PRINT"WHERE h IS THE STEP SIZE"
 9090 PRINT"AND N IS THE NO. OF STEPS"
 9100 RETURN
OK
