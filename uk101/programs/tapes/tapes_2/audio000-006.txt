

 10 DEF FNA(R)=RND(1)+T/10-INT(RND(0)+T/10)
 20 GOSUB8000:GOSUB5000
 30 GOTO1000
 100 REM P MOVE
 110 IFI=0THENPOKEK,254:IF(PEEK(K)AND4)=0THENDR(I)=(DR(I)-1)AND3
 120 IFI=1THENPOKEK,253:IF(PEEK(K)AND8)=0THENDR(I)=(DR(I)-1)AND3
 130 IFI=0THENPOKEK,253:IF(PEEK(K)AND32)=0THENDR(I)=(DR(I)+1)AND3
 140 IFI=1THENPOKEK,254:IF(PEEK(K)AND2)=0THENDR(I)=(DR(I)+1)AND3
 150 POKESC+X(I)+Y(I)*64,C(I)
 160 X(I)=X(I)+XI(DR(I)):Y(I)=Y(I)+YI(DR(I))
 162 IFX(I)<0THENX(I)=47
 164 IFX(I)>47THENX(I)=0
 166 Y(I)=(Y(I)AND15)
 170 P=SC+X(I)+Y(I)*64:IFPEEK(P)=32THEN200
 180 PRINTCHR$(12):PRINT"END OF GAME!"
 185 IFNP=1THENPRINT:PRINT"I WIN!":END
 190 IFI=0THENPRINT:PRINT"RIGHT PERSON WINS!":END
 195 PRINT:PRINT"LEFT PERSON WINS!":END
 200 POKEP,236+DR(I):RETURN
 299 REM C MOVE
 300 POKESC+X(1)+Y(1)*64,C(1)
 310 IFFNA(1)<.5THENDR(1)=(DR(0)+2)AND3:GOTO350
 320 IFFNA(1)>.1THEN350
 330 IFFNA(1)<.5THENDR(1)=(DR(1)-1)AND3:GOTO350
 340 DR(1)=(DR(1)+1)AND3
 350 P=SC+X(1)+XI(DR(1))+(Y(1)+YI(DR(1)))*64
 360 IFPEEK(P)=32THEN500
 370 FORJ=0TO3:X=X(1)+XI(J):Y=Y(1)+YI(J)
 372 IFX<0THENX=47
 374 IFX>47THENX=0
 376 Y=YAND15:P1=SC+X+Y*64
 380 IFPEEK(P1)=32THENDR(1)=J:GOTO500
 390 NEXT
 400 PRINTCHR$(12):PRINT"END OF GAME!"
 410 PRINT:PRINT"YOU WIN!":PRINT:END
 500 X(1)=X(1)+XI(DR(1)):Y(1)=Y(1)+YI(DR(1))
 510 IFX(1)<0THENX(1)=47
 520 IFX(1)>47THENX(1)=0
 530 Y(1)=Y(1)AND15
 540 POKESC+X(1)+Y(1)*64,236+DR(1)
 550 RETURN
 1000 I=0:GOSUB100
 1010 FORJ=1TOZ:NEXTJ
 1020 IFNP=2THENI=1:GOSUB100
 1025 IFNP=1THENGOSUB300
 1030 FORJ=1TOZ:NEXTJ
 1040 T=T+RND(1)
 1100 GOTO1000
 5000 INPUT"DO YOU WANT SIDES TO THE SCREEN";Q$
 5002 INPUT"SPEED(1-10)";S:S=INT(ABS(S)):IFS>10THEN5002
 5003 INPUT"NO. OF PLAYORS(1-2)";NP:IF(NP-1)*(NP-2)<>0THEN5003
 5005 PRINTCHR$(12);:POKE53261,32
 5007 SC=53261:K=57088
 5008 DIMX(1),Y(1),DR(1),C(1),CD(7),XI(3),YI(3)
 5010 IFLEFT$(Q$,1)="Y"THENSD=1
 5100 XI(1)=1:XI(3)=-1:YI(0)=-1:YI(2)=1
 5110 C(0)=161:C(1)=187:DR(0)=1:DR(1)=3
 5120 X(0)=2:X(1)=42:Y(0)=8:Y(1)=8
 5130 CD(0)=160:CD(1)=159:CD(2)=158:CD(3)=154
 5140 CD(4)=150:CD(5)=144:CD(6)=128:CD(7)=32
 5160 Z=50*(10-S)
 5190 POKE530,1
 5195 PRINTCHR$(12);:POKE53261,32
 5200 PRINT:PRINT"COUNTDOWN:-":FORI=1TO2000:NEXT
 5210 PRINTCHR$(12);:POKE53261,32
 5220 FORY=15TO0STEP-1:POKESC+22+Y*64,161:NEXT
 5230 FORY=0TO15:FORX=0TO7
 5240 POKESC+22+Y*64,CD(X)
 5250 FORI=1TO50:NEXT
 5260 NEXTX,Y
 5500 IFSD=0THENRETURN
 5510 FORI=0TO15:POKESC+I*64,161:POKESC+I*64+47,161:NEXT
 5520 FORI=0TO47:POKESC+I,161:POKESC+I+960,161:NEXT
 5600 RETURN
 8000 PRINTCHR$(12);:PRINT" SURROUND!":PRINT" ---------":PRINT
 8010 PRINT" YOU MUST DRIVE YOUR PLANE SO THAT YOU"
 8020 PRINT"MISS THE TRAILS LEFT BY THE PLANES"
 8030 PRINT"CONTROL KEYS ARE:-"
 8040 PRINT"           LEFT PLAYOR    RIGHT PLAYOR"
 8050 PRINT"TURN LEFT   L.SHIFT        '/'"
 8060 PRINT"TURN RIGHT  'Z'            R.SHIFT"
 8070 PRINT"WITH ONE PLAYOR, COMPUTER WILL CONTROL"
 8080 PRINT"THE RIGHT HAND PLANE"
 8100 RETURN
OK
