

 10 GOSUB9000:GOSUB5000:GOTO500
 20 FORJ=1TOZ:NEXT:IFM(I)=0THEN50
 21 DS(I)=DS(I)+1:IFDS(I)>30THENPOKESC+M(I),32:M(I)=0
 25 IFST=1THENMD(I)=T(I)
 30 P=SC+M(I):POKEP,32:P=P+D(MD(I)+8):H=PEEK(P)
 31 IFPR=0THEN40
 32 Q=P-SC:R=P(1-I):S=ABS(INT(Q/64)-INT(R/64))
 33 IFS<2ANDABS((Q-R)AND63)<2THENGOSUB4000:GOTO50
 40 IFH=32THENM(I)=P-SC:POKEP,D(MD(I)):RETURN
 45 IFP-SC<>P(1-I)THENM(I)=0:RETURN
 47 GOSUB4000
 50 IFI=0THENPOKEK,191:IF(PEEK(K)AND4)=0THEN100
 60 IFI=1THENPOKEK,127:IF(PEEK(K)AND128)=0THEN100
 70 RETURN
 100 DS(I)=0:M(I)=P(I):MD(I)=T(I):RETURN
 199 REM LMOVE
 200 POKEK,254:IF(PEEK(K)AND4)=0THENT(1)=(T(1)-1)AND7
 210 POKEK,251:IF(PEEK(K)AND128)=0THENT(1)=(T(1)+1)AND7
 220 POKEK,253:IF(PEEK(K)AND32)>0THENRETURN
 230 P=SC+P(I):POKEP,32:H=PEEK(P+D(T(I)+8)):IFH<>32THENRETURN
 240 P(I)=P(I)+D(T(I)+8):POKESC+P(I),T(I)+16:RETURN
 299 REM RMOVE
 300 POKEK,254:IF(PEEK(K)AND2)=0THENT(0)=(T(0)+1)AND7
 310 POKEK,223:IF(PEEK(K)AND128)=0THENT(0)=(T(0)-1)AND7
 320 POKEK,253:IF(PEEK(K)AND8)>0THENRETURN
 330 GOTO230
 500 I=0:GOSUB20:I=1:GOSUB20:GOSUB200
 510 POKESC+P(I),T(I)+16
 520 I=0:GOSUB20:I=1:GOSUB20
 550 I=0:GOSUB20:I=1:GOSUB20:I=0:GOSUB300
 555 POKESC+P(I),T(I)+16
 560 I=0:GOSUB20:I=1:GOSUB20
 600 FORJ=1TOZ:NEXT:T=T+1:IFT<Z1THEN500
 610 PRINTCHR$(12):PRINT"END OF GAME"
 620 PRINT"LEFT PLAYOR SCORE:-";S(1)
 630 PRINT"RIGHT       SCORE:-";S(0)
 640 PRINT:END
 3999 REM TANKI HIT
 4000 FORII=0TO32:POKESC+P(1-I),RND(1)*256:NEXT
 4005 POKESC+P(1-I),32
 4010 S(I)=S(I)+1
 4020 P(1-I)=INT(RND(1)*12+3)*64+INT(RND(1)*45+3)
 4025 IFPEEK(SC+P(1-I))<>32THEN4020
 4030 T(1-I)=INT(RND(1)*8)
 4040 M(I)=0:RETURN
 5000 REM BOARD
 5001 INPUT"GUIDED MISSILES";Q$:IFLEFT$(Q$,1)="Y"THENST=1
 5002 INPUT"SPEED(1-10)";S:S=INT(ABS(S)):IFS>10THEN5002
 5003 INPUT"PROXIMITY MISSILES";Q$:IFLEFT$(Q$,1)="Y"THENPR=1
 5004 INPUT"BARRIERS WANTED";Q$
 5005 DIMT(1),P(1),M(1),D(15),S(1),DS(1)
 5010 PRINTCHR$(12);
 5013 Z=(11-S)*10:Z1=10000/Z
 5020 SC=53196:K=57088
 5030 FORI=1TO48:POKESC+I+64,161:POKESC+I+1024,161:NEXT
 5040 FORI=1TO16:POKESC+I*64+1,161:POKESC+I*64+48,161:NEXT
 5100 T(1)=2:T(0)=6:P(1)=515:P(0)=557
 5110 MD(0)=0:MD(1)=1:M(0)=0:M(1)=0
 5120 D(0)=139:D(1)=189:D(2)=131:D(3)=190
 5130 D(4)=139:D(5)=189:D(6)=131:D(7)=190
 5140 D(8)=-64:D(9)=-63:D(10)=1:D(11)=65
 5150 D(12)=64:D(13)=63:D(14)=-1:D(15)=-65
 5160 POKE530,1
 5500 IFLEFT$(Q$,1)<>"Y"THENRETURN
 6000 FORI=6TO11:POKESC+7+I*64,161:POKESC+41+I*64,161
 6010 IFI=6ORI=11THENPOKESC+6+I*64,161:POKESC+42+I*64,161
 6015 POKESC+24+I*64,161
 6020 NEXT
 6030 FORI=15TO18:POKESC+512+I,161:POKESC+527+I,161:NEXT
 6500 RETURN
 9000 PRINTCHR$(12):PRINT" TANK BATTLE!":PRINT" ------------":PRINT
 9010 PRINT"                 LEFT       RIGHT PLAYOR"
 9020 PRINT:PRINT"ROTATE LEFT:     L.SHIFT        '.'"
 9030 PRINT"ROTATE RIGHT:    'X'            R.SHIFT"
 9040 PRINT"MOVE FOREWARD:   'Z'            '/'"
 9050 PRINT"FIRE:            '1'         RUB OUT"
 9060 PRINT:PRINT" THE ABOVE ARE THE KEYS FOR EACH PLAYOR.":PRINT
 9070 RETURN
OK
