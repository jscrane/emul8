

 10 PRINTCHR$(12):PRINT:PRINT"BIORHYTHMS":PRINT"----------"
 20 PRINT:PRINT:INPUT"DO YOU NEED INSTRUCTIONS";Q$
 25 P=6.28318:DIMP(3):P(1)=23/P:P(2)=28/P:P(3)=33/P
 28 POKE11,0:POKE12,253
 30 IFLEFT$(Q$,1)="Y"THENGOSUB9000
 40 PRINT"DATE OF BIRTH";:GOSUB8000
 50 T1=ND:PRINT"TODAYS DATE";:GOSUB8000:ND=ND-T1
 55 LN=ND
 70 PRINT"WHICH CYCLE:-":PRINT" 1:PHYSICAL":PRINT" 2:EMOTIONAL"
 80 PRINT" 3:INTELLECTUAL":PRINT" 4:AVERAGE CYCLE"
 90 INPUTN:N=INT(ABS(N)):IFN>4THENPRINT"TYPE 1, 2, 3 OR 4":GOTO90
 95 PRINTCHR$(12);
 102 PRINT"      ";:IFN=1THENPRINT"PHYSICAL";
 104 IFN=2THENPRINT"EMOTIONAL";
 106 IFN=3THENPRINT"INTELLECTUAL";
 108 IFN=4THENPRINT"AVERAGE";
 109 PRINT" CYCLE:-"
 110 SC=53196:MS=53772
 120 FORI=1TO14:PRINT:NEXT
 130 FORI=0TO48:POKEMS+I,45:NEXT
 140 FORI=2TO16:M=INT(100.5-(I-2)/14*200)
 150 M$=RIGHT$("   "+STR$(M),4):FORJ=1TO4
 160 POKE(SC+I*64-1+J),ASC(MID$(M$,J,1)):NEXTJ,I
 170 FORI=0TO45STEP5:M$=MID$(STR$(I)+"  ",2,2)
 180 POKE(MS+4+I),ASC(M$)
 190 POKE(MS+5+I),ASC(RIGHT$(M$,1)):NEXT
 200 POKE(MS+5),45:POKE(MS+10),45
 300 FORX=0TO45:IFN<4THENY=SIN((ND+X)/P(N))*7.5
 310 IFN=4THENY=SIN((ND+X)/P(1))+SIN((ND+X)/P(2))+SIN((ND+X)/P(3))
 320 IFN=4THENY=Y/3*7.5
 330 Y=Y+.5:Y1=(Y-INT(Y))*8:Y=INT(Y)
 340 POKE(MS+4+X-Y*64),Y1+128:NEXT
 400 X=USR(0):A=PEEK(531)
 410 IFA=32THENND=ND+40:GOTO95
 420 IFA<>69THENND=LN:GOTO70
 430 END
 8000 INPUT" (DAY,MONTH,YEAR)";D,M,Y
 8010 IFD<1ORD>31ORM<1ORM>12THEN8000
 8020 IFY<100THENY=Y+1900
 8030 IFY<1900THEND=D+1
 8040 N=1:IFM<3THENN=13:Y=Y-1
 8050 ND=INT((N+M)*30.6+INT(Y*365.25)+D)
 8060 RETURN
 9000 PRINT:PRINT"THE THEORY OF BIORHYTHMS STATES THAT"
 9010 PRINT"THERE ARE THREE CYCLES THAT STARTED ON THE DAY"
 9020 PRINT"YOU WERE BORN":PRINT" 1 THE PHYSICAL CYCLE-23 DAYS LONG"
 9030 PRINT" 2 THE EMOTIONAL CYCLE-28 DAYS LONG"
 9040 PRINT" 3 THE INTELLECTUAL CYCLE-33 DAYS LONG"
 9050 PRINT:PRINT"THE PROGRAM PLOTS A GRAPH OF YOUR BIORHYTHMS"
 9060 PRINT"POSITIVE VALUES ARE 'UP' DAYS"
 9070 PRINT"AND NEGATIVE CYCLES ARE 'DOWN' DAYS"
 9080 PRINT"WITH ONE DAY PER COLUMN"
 9090 PRINT"THEN PRESS SPACE TO CONTINUE AFTER 40 DAYS"
 9100 PRINT"OR PRESS E TO END"
 9110 PRINT"PRESS ANY OTHER KEY TO CHANGE DISPLAYED CYCLE":RETURN
 9999 GOTO 9999
OK
