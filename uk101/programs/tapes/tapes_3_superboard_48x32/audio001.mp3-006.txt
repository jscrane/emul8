

 10 PRINTCHR$(26):PRINT:PRINT:PRINT"          NIM!!!"
 20 PRINT"          ------":PRINT
 21 PRINT:PRINT"THIS IS THE USUAL GAME OF NIM"
 22 PRINT"WITH THREE PILES"
 23 PRINT"PLAYORS TAKE TURNS TO REMOVE":PRINT"STICKS FROM ANY PILE"
 24 PRINT"THE PERSON WHO TAKES THE LAST STICK WINS":PRINT
 25 DIM P(5):INPUT"STANDARD GAME";Q$
 27 IF LEFT$(Q$,1)="N"THEN 40
 30 P(1)=7:P(2)=5:P(3)=3:GOTO 70
 40 PRINT"RANDOM GAME":PRINT:INPUT"MAX. NO. PER PILE";N
 50 IFN>255THENPRINT"TOO MANY":GOTO40
 60 FORI=1TO3:P(I)=INT(RND(45)*N)+1:NEXT
 70 INPUT"DIFFICULTY 1(EASY) TO 10(VERY HARD)";DF
 75 G=INT(RND(1)*2)
 80 FORI=1 TO 3:PRINT"PILE";I;":-";P(I):NEXT
 90 G=1-G:IF G=1 THEN 150
 100 PRINT"YOUR GO":INPUT"PILE";N:INPUT"REMOVE";X
 110 IF P(N)-X<0 OR X<1 THEN PRINT"ILLEGAL MOVE":GOTO 100
 120 P(N)=P(N)-X
 130 IF P(1)+P(2)+P(3)=0 THEN PRINT"YOU WIN":GOTO 300
 140 GOTO 80
 150 PRINT"MY GO":P(4)=P(1):P(5)=P(2)
 151 IFRND(1)*11>DFTHEN2000
 155 NI=INT(RND(1)*3)+1:FORI1=NI TO NI+2
 160 I=INT(.5+(I1/3-INT(I1/3))*3)+1
 165 N=(P(I) OR P(I+1))-(P(I) AND P(I+1))
 170 IF P(I+2)<=N THEN 200
 180 I=INT(1.5+((I+1)/3-INT((I+1)/3))*3)
 185 P(I)=N
 190 IF P(1)+P(2)+P(3)=0THENPRINT"I WIN":GOTO300
 195 GOTO 80
 200 NEXT I1
 210 R=INT(RND(1)*3)+1:N=INT(RND(1)^2*P(R))+1
 220 IF P(R)=0 THEN 210
 230 P(R)=P(R)-N:GOTO 80
 300 PRINT:INPUT"ANOTHER GAME";Q$:IFLEFT$(Q$,1)="Y"THENRUN25
 310 PRINT"THANK YOU":END
 2000 P=INT(RND(1)*3)+1:IFP(P)=0THEN2000
 2010 N=INT(RND(1)*P(P))+1:P(P)=P(P)-N
 2030 GOTO190
OK
