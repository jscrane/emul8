

 10 PRINTCHR$(26):PRINT:PRINT"DUNGEON ADVENTURE!!!"
 20 PRINT"--------------------":PRINT:PRINT
 40 CLEAR:HP=100:SP=10:LV=1
 45 INPUT"RANDOM NO(0-1)";RN:RN=RN+RND(1)
 50 NM=330:DIMA(11)
 60 DEFFNR(X)=INT(((RND(1)+RN)-INT(RND(0)+RN))*X)+1
 100 PRINT"ON THE QUEST FOR ADVENTURE YOU ARRIVE AT"
 110 PRINT"THE RUINED CASTLE UNDER WHICH ARE RUMOURED"
 120 PRINT"TO LIE DUNGEONS CONTAINING GREAT TREASURE"
 130 PRINT"AND TERRIBLE MONSTERS. AT THE RUINS"
 260 PRINT"YOU FIND A DARK GAPING HOLE WITH STEPS LEADING"
 270 PRINT"DOWN INTO THE HILLSIDE. YOU LIGHT YOUR TOURCH"
 280 PRINT"GRIP YOUR SWORD, AND DESCEND!"
 295 PRINT:PRINT
 300 PRINT"TO ENTER YOUR COMMANDS SIMPLY TYPE THE FIRST"
 310 PRINT"LETTER AND RETURN.(EG TO GO LEFT TYPE L)"
 320 PRINT"TO SEE YOUR STATUS TYPE S"
 325 PRINT"TO CAST A HEALING SPELL TYPE H"
 330 PRINT"MAGIC ITEMS WILL ADD TO YOUR"
 340 PRINT"STRENGTH OR NUMBER OF SPELLS."
 350 PRINT"REMEMBER: NO TURNING BACK!!"
 360 PRINT:PRINT"(BUT IF YOU ARE IN GREAT DANGER TRY THE"
 370 PRINT"MAGIC WORD 'SHAZAM')"
 400 PRINT:PRINT"AT THE BOTTOM YOU SEE:-"
 999 REM CREATE AREA
 1000 FORI=0TO2:A=FNR(7):IFFNR(100)<80THENA=FNR(4)
 1001 GOSUB9999
 1010 IFA=7ANDFNR(10)>3THENA=FNR(4)
 1020 A(I+3)=0:A(I+6)=0:A(I+9)=0
 1030 IFFNR(3)=1THEN1100
 1035 GOSUB9999
 1040 IFFNR(2)=1THENA(I+6)=FNR(500):A(I+9)=-(FNR(10)=1):GOTO1100
 1050 GOSUB5000:A(I+3)=N
 1060 A(I+6)=INT(RND(1)*T1*(1+LK/100))
 1070 IFT=0THENT=-(FNR(10)=1)
 1080 A(I+9)=T
 1100 A(I)=A:NEXTI
 1101 FORI=1TO1000:NEXT
 1110 IFA(0)=A(1)ANDA(1)=A(2)THEN1120
 1111 GOTO2000
 1120 LV=LV+1:PRINT"YOU FELL INTO A PIT";:D=FNR(10)
 1130 PRINTD*10;"FEET DEEP,":D1=D*FNR(6)
 1140 PRINT"TAKING";D1;"HP'S DAMAGE":HP=HP-D1
 1145 IFHP<0THEN1900
 1150 IFFNR(5)>4THEN1315
 1160 PRINT"AT THE BOTTOM IS":ONFNR(4)GOTO1200,1300,1400
 1180 PRINT"A SMELLY 'PUDDLE' (UGH!!)":GOTO1500
 1200 PRINT"A POOL OF ACID, WHICH DOES";:D1=FNR(8)
 1210 PRINTD1;"HP'S OF DAMAGE":GOTO1315
 1300 PRINT"A LOT OF NASTY SPIKES,":PRINT"YOU HIT";:N1=FNR(8)
 1310 PRINTN1;"OF THEM, FOR";:D1=FNR(4)*N1:PRINTD1;"HP'S DAMAGE"
 1315 HP=HP-D1:IFHP<0THEN1900
 1317 GOTO1500
 1320 HP=HP-D1:GOTO1900
 1400 PRINT"A(N) ";:GOSUB5000:DM=HT:PRINTN$:GOSUB7500:GOTO1900
 1500 IFFNR(6)>1THEN1000
 1510 GOSUB1960:GOTO1000
 1900 IFHP<0THENPRINT"YOU ARE DEAD!!":GOTO2800
 1949 REM WANDERER
 1950 IFFNR(6)>1THEN2000
 1955 GOSUB1960:GOTO1900
 1960 PRINT"YOU MEET A(N) ";:GOSUB5000:PRINTN$:DM=HT:GOSUB7500:RETURN
 2000 PRINT"LEFT","FORWARDS","RIGHT"
 2010 FORI=0TO2:IFA(I)=1THENPRINT"CORRIDOR",
 2020 IFA(I)=2THENPRINT"DOOR",
 2030 IFA(I)=3THENPRINT"BLANK WALL",
 2040 IFA(I)=4THENPRINT"ROOM ENTRANCE",
 2050 IFA(I)=5THENPRINT"STAIRS UP",
 2060 IFA(I)=6THENPRINT"STAIRS DOWN",
 2070 IFA(I)=7THENPRINT"EXIT",
 2080 NEXT:PRINT
 2085 INPUT"WHICH WAY(L, F OR R)";A$
 2090 A$=LEFT$(A$,1):A=1:IFA$="L"THENA=0
 2091 IFA$<>"H"THEN2095
 2092 IFSP<1THENPRINT"YOU HAVE NO SPELLS":GOTO2085
 2093 SP=SP-1:HP=HP+5+FNR(5):PRINT"HEALED":GOTO2085
 2095 IFA$="S"THENGOSUB8000:GOTO2085
 2100 IFA$="R"THENA=2
 2110 F=A(A):I=A:ONFGOTO2200,2300,2400,2500,2600,2700,2800
 2200 GOTO1500
 2300 INPUT"DOOR...LISTEN OR OPEN";A$:A$=LEFT$(A$,1)
 2310 IFA$="S"THENGOSUB8000:GOTO2300
 2320 IFA$="O"THEN2500
 2340 PRINT"YOU HEAR ";:IFFNR(2)=1ORA(A+3)=0THENPRINT"NOTHING":GOTO2360
 2350 N=A(I+3):GOSUB6000:PRINT"'S MAKING NOISES"
 2360 IFFNR(100)>5THEN2390
 2370 PRINT"A ROT GRUB BURROWS INTO YOUR EAR"
 2380 D1=FNR(20):PRINT"AND DOES";D1;"HP'S DAMAGE":GOTO1320
 2390 INPUT"ARE YOU GOING TO OPEN IT";A$:A$=LEFT$(A$,1)
 2395 IFA$="Y"THEN2500
 2396 PRINT"...CHICKEN!":GOTO2000
 2400 PRINT"YOU CAN'T GO THAT WAY!!":FORII=1TO2000:NEXT
 2410 IFFNR(6)>1THEN1950
 2420 PRINT"BUT AS YOU BOUNCE OFF THE WALL..."
 2430 PRINT"IT FALLS ON YOU FOR";:D1=FNR(6)*FNR(6)
 2440 PRINTD1;"HP'S DAMAGE":GOTO1320
 2500 N=A(I+3):PRINT"THE ROOM CONTAINS ";
 2510 IFA(I+6)>0THENPRINT"TREASURE ";
 2520 IFN>0ANDA(I+6)>0THENPRINT"AND ";
 2521 DM=HT
 2525 IFN>0THENPRINT"A(N) ";:GOSUB6000:DM=HT
 2530 IFN=0ANDA(I+6)=0THENPRINT"NOTHING":GOTO1000
 2540 PRINT:INPUT"GO IN, LEAVE OR HELP";A$
 2550 A$=LEFT$(A$,1):IFA$="G"THEN7000
 2560 IFA$="L"THEN2580
 2565 IFA$="S"THENGOSUB8000:GOTO2540
 2570 PRINT"THE ";:GOSUB6000:PRINT" HAS";HT;"HP'S":GOTO2540
 2580 IFFNR(6)>1THENGOTO2396
 2590 PRINT"TOUGH LUCK...HE ATTACKS!":GOSUB7500:GOSUB9000:GOTO1500
 2600 LV=LV+(LV>1):GOTO1500
 2700 LV=LV+1:GOTO1500
 2800 IFF=7THENPRINT"WELL DONE, YOU GOT OUT ALIVE!"
 2805 GOSUB8000:PRINT"YOU HAVE SCORED:";
 2810 PRINTLV*M1+GP/100;"POINTS":STOP
 4999 REM SELECT MONSTER
 5000 RN=1/RN+RND(1):RN=RN-INT(RN)
 5004 IFLV>15THENN=INT(NM*(1-RN/(LV-12))):GOTO5006
 5005 N=INT(NM*(RN/3+LV*2/45))
 5006 IFFNR(10)=1THENN=INT(NM*RN)
 5010 RESTORE:N1=0
 5020 READN$,PR,HT,T1,T:N1=N1+PR:IFN1<NTHEN5020
 5030 RETURN
 5999 REM PRINT NAME N
 6000 GOSUB5010:PRINTN$;:RETURN
 6999 REM FIGHT
 7000 IFN=0THEN7410
 7005 PRINT:PRINT"NOW WHAT?":INPUT"COMBAT, SPELL OR RETREAT";A$
 7006 IFA$="SHAZAM"ANDUS=0THENUS=1:HP=FNR(100):SP=FNR(10):GOTO1000
 7007 IFA$="SHAZAM"ANDUS=1THENPRINT"NOTHING HAPPENS":GOTO7000
 7010 A$=LEFT$(A$,1):IFA$="R"THEN2580
 7020 IFA$="C"THENGOSUB7500:GOSUB9000:GOTO1000
 7030 IFA$<>"S"THEN7000
 7040 IFSP<1THENPRINT"ER...YOU DON'T SEEM TO HAVE ANY":GOTO7000
 7050 SP=SP-1:DM=DM-FNR(21):PRINT"THE NOW SPELL-BLASTED ";
 7055 GOSUB6000:PRINT
 7060 IFDM<1THENPRINT"LIES DEAD ON THE FLOOR":GOTO7400
 7070 PRINT"IS VERY ANGRY, HE ADVANCES":IFFNR(2)=1THEN7000
 7080 PRINT"HE ATTACKS...":GOSUB7500:GOTO7400
 7400 M1=M1+1
 7410 GOSUB9000:GOTO1500
 7500 PRINT"YOU HAD A TERRIFIC BATTLE WITH THE ";:GOSUB6000
 7510 PRINT:HP=HP-DM:IFHP>0THEN7600
 7520 PRINT"BUT HE KILLED YOU AND TOOK ALL YOUR TREASURE"
 7530 GP=0:GOTO2800
 7600 PRINT"AND YOU KILLED HIM":M1=M1+1:RETURN
 8000 PRINT:PRINT"YOU HAVE";HP;"HP'S"
 8010 PRINT"AND";SP;"SPELLS":PRINT"AND";GP;"GP'S"
 8020 PRINT"YOU ARE ON LEVEL";LV
 8030 PRINT"YOU HAVE KILLED";M1;"MONSTERS!":
 8040 IFLK>0THENPRINT"YOU ALSO HAVE AN";LK;"% LUCKSTONE"
 8060 RETURN
 8999 REM GET TREASURE
 9000 T1=A(A+6):T=A(A+9):IFT1+T=0THENRETURN
 9010 PRINT"THERE ARE";T1;"GP'S":GP=GP+T1
 9015 IFT=0THENT=T-(FNR(6)=3)
 9017 IFM1*RND(1)>20THENT=INT(.5+T*30/M1/RND(0))
 9020 IFT=0THENRETURN
 9030 FORJ=1TOT:G=FNR(14):PRINT"+ A";
 9040 IFG=1THENPRINT" SUIT OF ARMOUR":HP=HP+FNR(100)
 9050 IFG=2THENPRINT" SHIELD":HP=HP+FNR(80)
 9060 IFG=3THENPRINT" POTION":HP=HP+FNR(50)
 9070 IFG=4THENPRINT" STAFF":HP=HP+FNR(10):SP=SP+FNR(10)
 9080 IFG=5THENPRINT" SPELL BOOK":SP=SP+FNR(12)
 9090 IFG=6THENPRINT" SCROLL":SP=SP+FNR(8)
 9100 IFG=7THENPRINT" WAND":SP=SP+FNR(15)
 9110 IFG=8THENPRINT" RING":GOSUB9500
 9120 IFG=9THENPRINT" BOOK":GOSUB9500
 9130 IFG=10THENPRINT"N ARTIFACT":HP=HP+FNR(100):SP=SP+FNR(12)
 9140 IFG=11THENPRINT" NECKLACE":SP=SP+FNR(12)
 9150 IFG=12THENPRINT" SWORD":HP=HP+FNR(120)
 9160 IFG=13THENPRINT" HELM":HP=HP+FNR(50)
 9170 IFG<14THENNEXT:RETURN
 9180 PRINT" LUCKSTONE";:L=INT(RND(1)*RND(1)*100)
 9190 PRINTL;"%":IFL>LKTHENLK=L
 9200 NEXT:RETURN
 9500 IFFNR(2)=1THEN9520
 9510 SP=SP+FNR(10):RETURN
 9520 HP=HP+FNR(80):RETURN
 9999 RN=1/RN+RND(1):RN=RN-INT(RN):RETURN
 30000 DATAKOBOLD,20,1,10,0
 30010 DATAGOBLIN,20,5,100,0
 30015 DATAORC,15,2,500,0
 30020 DATAZOMBIE,10,8,0,0
 30030 DATAHELL-HOUND,8,12,1000,0
 30050 DATAWERE-RAT,8,8,2000,0
 30070 DATAWIGHT,8,8,8000,0
 30080 DATAHOBGOBLIN,10,6,1000,0
 30090 DATAWERE-WOLF,8,8,2000,0
 30100 DATABASALISK,6,10,6000,0
 30110 DATAWRAITH,6,10,8000,0
 30120 DATAGARGOYLE,5,10,1000,0
 30130 DATAMEDUSA,5,15,12000,1
 30140 DATAMUMMY,5,12,5000,0
 30150 DATATYPE I DEMON,4,10,10000,0
 30160 DATATYPE II DEMON,4,20,20000,0
 30170 DATAVAMPIRE,5,20,100000,0
 30180 DATATROLL,6,16,6000,0
 30190 DATASALAMANDER,5,20,9000,2
 30200 DATAGHOST,5,32,8000,1
 30210 DATAETTIN,5,34,12000,0
 30220 DATACLAY GOLEM,5,30,0,0
 30230 DATAFLESH GOLEM,5,32,0,0
 30240 DATASTONE GOLEM,5,24,0,0
 30260 DATAHILL GIANT,5,16,3000,0
 30270 DATAFROST GIANT,4,24,8000,0
 30280 DATASTONE GIANT,4,18,4000,0
 30290 DATAERINYES,5,16,40000,0
 30295 DATATYPE III DEMON,4,30,30000,0
 30300 DATAHORNED DEVIL,4,35,5000,0
 30310 DATABONE DEVIL,4,32,0,0
 30320 DATAAERIAL SERVANT,5,40,0,0
 30330 DATABEHOLDER,5,42,15000,3
 30340 DATAMANTICORE,4,48,8000,1
 30350 DATAMIND FLAYOR,4,48,4000,2
 30360 DATAUMBER HULK,4,34,40000,2
 30370 DATAXORN,3,34,40000,2
 30380 DATATYPE IV DEMON,4,40,40000,1
 30390 DATATYPE V DEMON,4,50,50000,2
 30400 DATABARBED DEVIL,4,32,0,0
 30410 DATAFIRE GIANT,5,30,8000,0
 30420 DATACLOUD GIANT,5,36,9000,0
 30430 DATAFIRE ELEMENTAL,4,48,0,0
 30440 DATAHYDRA,4,50,4000,0
 30450 DATAPURPLE WORM,4,56,9000,2
 30460 DATAWHITE DRAGON,4,40,50000,1
 30470 DATABLACK DRAGON,4,50,60000,2
 30480 DATASTORM GIANT,5,42,110000,1
 30490 DATAWATER ELEMENTAL,4,60,0,0
 30500 DATATYPE VI DEMON,4,60,60000,3
 30510 DATAICE DEVIL,4,60,10000,3
 30520 DATAPITFIEND,3,65,12000,4
 30530 DATAGERYON,2,50,40000,2
 30540 DATABLUE DRAGON,3,60,70000,2
 30550 DATAGREEN DRAGON,3,70,80000,3
 30560 DATADISPATER,2,70,60000,3
 30570 DATABAALZEBUL,2,80,80000,5
 30580 DATARED DRAGON,3,80,90000,4
 30590 DATAORCUS,2,105,150000,7
 30600 DATAIRON GOLEM,4,90,0,0
 30610 DATALICH,3,90,85000,5
 30620 DATABAHAMUT,1,120,150000,8
 30630 DATATIAMAT,1,110,140000,7
 30640 DATAASMODIUS,3,110,170000,7
OK
