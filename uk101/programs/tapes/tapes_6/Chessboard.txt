

 10 FORI=1TO16:PRINT:NEXT:PRINT"CHESSBOARD":PRINT"----------":PRINT
 20 PRINT"THIS PROGRAM DISPLAYES A CHESSBOARD AND"
 30 PRINT"PIECES ON THE SCREEN."
 40 PRINT"YOU INPUT YOUR MOVES IN STANDARD NOTATION"
 50 PRINT"(D2-D4 ETC.) AND THE COMPUTER MOVES"
 60 PRINT"YOUR PIECES AND DISPLAYS THE MOVES."
 70 PRINT"PAWNS ARE PROMOTED TO QUEENS IF THEY"
 80 PRINT"REACH THE OTHER SIDE"
 90 PRINT:PRINT:POKE 530,1:POKE 57088,253
 95 PRINT"PRESS SPACE BAR TO START";:WAIT 57088,255,255
 100 FORI=1TO16:PRINT:NEXT:SC=53264
 105 POKE530,0:POKE 54221,32
 110 FORI=0TO31:FORJ=0TO15:N=32
 120 IF(INT(I/4)/2-INT(I/8))=(INT(J/2)/2-INT(J/4))THEN N=187
 130 IFN=187THENPOKE(SC+I+J*64),N
 140 NEXT J,I
 150 DATA A7A7R6R5P7P6W7W8S9T0P7P6R6R5A7A7
 160 DATA U9U8W7W8W7W8W7W8U9U8W7W8W7W8U9U8
 170 DATA A7A7R6R5P7P6X4X5R6R8P7P6R6R5A7A7
 180 DATA Q1Q1X4X5X4X5X4X5Q1Q1X4X5X4X5Q1Q1
 200 Y=0:GOSUB1000:Y=1:GOSUB1000
 210 Y=14:GOSUB1000:Y=15:GOSUB1000
 250 A=204:B=205:Y=2:GOSUB1100
 260 A=195:B=197:Y=3:GOSUB1100
 270 A=165:B=167:Y=12:GOSUB1100
 280 A=195:B=197:Y=13:GOSUB1100
 300 Y=15:FORI=0TO28STEP4
 310 POKE(SC+Y*64+I),65+I/4:NEXT
 320 X=-1:FOR Y=0 TO 15 STEP 2
 330 POKE(SC+Y*64+X),56-Y/2:NEXT
 340 A$="White Black":FORI=1TO11
 350 POKE(SC+33+I),ASC(MID$(A$,I,1)):NEXT
 360 POKE 11,0:POKE 12,253:NN=1:LN=0
 500 N$=RIGHT$(STR$(NN),2)
 510 POKE(SC+96+LN),ASC(LEFT$(N$,1))
 520 POKE(SC+97+LN),ASC(RIGHT$(N$,1))
 530 PP=SC+98+LN:GOSUB 900:GOSUB 2000
 540 PP=SC+104+LN:GOSUB 900:GOSUB 2000
 550 NN=NN+1:LN=LN+64:IF LN>900 THEN LN=0
 555 GOTO 500
 900 REM INPUT GO ROUTINE
 910 FORI=0TO4:POKE(PP+I),32:NEXT
 920 X=USR(0):G=PEEK(531)
 930 IF G<65 OR G>72 THEN 910
 940 POKE(PP),G:A=G-65:X=USR(0):G=PEEK(531)
 950 IF G<49 OR G>56 THEN 910
 960 POKE(PP+1),G:B=56-G:POKE(PP+2),45
 970 X=USR(0):G=PEEK(531)
 975 IF G<65 OR G>72 THEN 910
 980 POKE(PP+3),G:C=G-65:X=USR(0):G=PEEK(531)
 985 IFG<49 OR G>56 THEN 910
 990 POKE(PP+4),G:D=56-G:RETURN
 1000 REM OUTPUT LINE
 1010 READ A$:X=1:FORI=1TO8:B$=MID$(A$,I*4-3,4)
 1020 N=(ASC(LEFT$(B$,1))-65)*10+VAL(MID$(B$,2,1))
 1030 POKE(SC+Y*64+X),N
 1040 N=(ASC(MID$(B$,3,1))-65)*10+VAL(MID$(B$,4,1))
 1050 POKE(SC+Y*64+X+1),N:X=X+4:NEXT I:RETURN
 1100 REM OUTPUT ROW OF PAWNS
 1110 X=1:FORI=1TO8:POKE(SC+Y*64+X),A
 1120 POKE(SC+Y*64+X+1),B:X=X+4:NEXT:RETURN
 2000 REM MOVE PIECE
 2010 X=A*4+1:Y=B*2:X1=C*4+1:Y1=D*2
 2020 FORI=0TO1:FORJ=0TO1
 2030 N=PEEK(SC+(Y+I)*64+X+J)
 2040 POKE(SC+(Y1+I)*64+X1+J),N:NEXT J,I
 2050 CO=PEEK(SC+X-1+Y*64)
 2060 FORI=0TO1:FORJ=0TO1
 2070 POKE(SC+(Y+I)*64+X+J),CO:NEXT J,I
 2080 N=PEEK(SC+X1+Y1*64):IFD=7ANDN=204THEN2200
 2090 IF D=0 AND N=165 THEN 2500
 2100 RETURN
 2200 PQ=SC+X1+Y1*64:POKE PQ,227:POKE PQ+1,228
 2210 POKE PQ+64,227:POKE PQ+65,228:RETURN
 2500 PQ=SC+X1+Y1*64:POKE PQ,234:POKE PQ+1,235
 2510 POKE PQ+64,234:POKE PQ+65,235:RETURN
OK
