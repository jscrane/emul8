

 10 FORI=1TO16:PRINT:NEXT:PRINT"DOCKING A SPACE SHIP"
 20 PRINT"--------------------":PRINT:PRINT
 30 INPUT"DO YOU NEED INSTRUCTIONS";Q$:IFLEFT$(Q$,1)="N"THEN1000
 40 PRINT"YOU ARE THE PILOT OF A SPACE SHIP WHICH"
 50 PRINT"YOU MUST DOCK WITH ANOTHER SHIP ("CHR$(181)")"
 60 PRINT"TO TURN YOUR SHIP CLOCKWISE PRESS RIGHT SHIFT"
 70 PRINT"TO TURN ANTICLOCKWISE PRESS LEFT SHIFT"
 80 PRINT"TO FIRE ENGINES PRESS SPACE BAR"
 90 PRINT"TO DOCK YOU MUST MATCH POSITION AND VELOCITY"
 100 PRINT:PRINT"PRESS SPACE BAR TO START";
 110 POKE 530,1:POKE 57088,253:WAIT 57088,255,255
 1000 SC=53196:X=24:Y=8:N=236
 1001 FORI=1TO16:PRINT:NEXT
 1002 VX=0:VY=0:G=.02:X1=0:Y1=0
 1003 V1=RND(1)*4-2:V2=RND(1)*2-1
 1005 KEY=57088:T=0:POKE 530,1
 1010 POKE(SC+INT(.5+X)+INT(.5+Y)*64),N
 1020 POKE(KEY),254:K=PEEK(KEY)
 1023 P=P+1:IFP=10THENP=0
 1024 IFK<>250ANDK<>252THENP=9
 1025 IFP>0THEN1050
 1030 IFK=250 THEN N=N-1:IFN=235THEN N=239
 1040 IFK=252 THEN N=N+1:IFN=240THEN N=236
 1050 POKE(KEY),253:K=PEEK(KEY)
 1060 IF K<>239 THEN 1200
 1070 IFN=236 THEN VY=VY-G/2
 1080 IFN=237 THEN VX=VX+G
 1090 IFN=238 THEN VY=VY+G/2
 1100 IFN=239 THEN VX=VX-G
 1200 POKE(SC+INT(.5+X)+INT(.5+Y)*64),32
 1210 X=X+VX:Y=Y+VY
 1220 IFINT(X+.5)<1THENX=X+46
 1230 IFINT(X+.5)>46THENX=X-46
 1240 IFINT(.5+Y)<1THENY=Y+16
 1250 IFINT(.5+Y)>16THENY=Y-16
 1260 POKE(SC+INT(.5+X)+INT(.5+Y)*64),N
 1270 POKE(SC+INT(.5+X1)+INT(.5+Y1)*64),32
 1280 X1=X1+V1:Y1=Y1+V2
 1282 IFINT(.5+X1)<1THENX1=X1+46
 1284 IFINT(.5+X1)>46THENX1=X1-46
 1286 IFINT(.5+Y1)<1THENY1=Y1+16
 1288 IFINT(.5+Y1)>16THENY1=Y1-16
 1290 POKE(SC+INT(.5+X1)+INT(.5+Y1)*64),181
 1295 T=T+1
 1310 IF ABS(X-X1)>1ORABS(Y-Y1)>1THEN1020
 1320 IFABS(VX-V1)>.04ORABS(VY-V2)>.025THEN1020
 1330 FORI=1TO16:PRINT:NEXT
 1340 PRINT"      DOCKED":PRINT:PRINT:PRINT
 1350 PRINT"YOU DOCKED IN";T/10;"SECS."
 1360 T=T/10:PRINT:IFT<30THENPRINT"EXCELLENT!!":GOTO1500
 1370 IFT<50THENPRINT"VERY GOOD DRIVING!":GOTO1500
 1380 IFT<70 THENPRINT"GOOD DRIVING":GOTO1500
 1385 IFT<100 THENPRINT"NOT BAD":GOTO1500
 1390 IFT<120THENPRINT"COULD BE BETTER":GOTO1500
 1400 IFT<200THENPRINT"YOU NEED LESSONS!":GOTO1500
 1410 PRINT"OH WELL, BETTER LUCK NEXT TIME!"
 1500 PRINT:INPUT"ANOTHER TRY";Q$
 1510 IFLEFT$(Q$,1)="Y"THEN1000
 1520 PRINT:PRINT"THANKYOU":PRINT:END
OK
