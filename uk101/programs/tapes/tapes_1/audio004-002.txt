

 10 GOSUB2000:PRINT"SPACE INVADERS!!!"
 20 PRINT"-----------------":PRINT:INPUT"INSTRUCTIONS";Q$
 30 IFLEFT$(Q$,1)="N"THEN100
 40 PRINT:PRINT:PRINT"THE OBJECT OF THIS GAME IS TO DESTROY"
 50 PRINT"AS MANY INVADERS AS YOU CAN"
 60 PRINT"YOU GET 10 POINTS FOR A SMALL (LOWER) INVADER
 70 PRINT"AND 50 POINTS FOR A LARGE (TOP ROW) INVADER"
 80 PRINT:PRINT"YOUR SCORE IS DISPLAYED TOP LEFT"
 90 PRINT"AND YOUR NUMBER OF BASES LEFT, TOP RIGHT"
 95 GOTO 9000
 100 PP=53196:PG=54220:PI=53326:POKE(530),1
 105 DIM A(8):S=0:N=3
 110 NI=-4:CI=0:GI=1:KEY=57088:GUN=INT(RND(1)*24)+1
 120 FORI=1TO16:PRINT:NEXT:POKE(PG+GUN),29
 130 H=PP+896:FOR I=1 TO 46 STEP 6
 150 POKE(H+I),15:POKE(H+I+1),15:POKE(H+I+2),15
 160 POKE64+H+I,14:POKE65+H+I,14:POKE66+H+I,14
 170 NEXT
 174 POKE(53290),29:POKE(53292),29
 175 FORI=0TO30STEP4:POKE(PI+I),237
 180 POKE(PI+I+1),239:NEXT
 190 FORI=1TO4:FORJ=0TO30STEP4
 200 POKE(PI+I*64+J),18:POKE(PI+I*64+J+1),22
 210 NEXT J,I:POKE(54221),32
 250 GOSUB1000:GOSUB600:GOSUB500:GOSUB600:GOSUB1000:GOSUB600:GOTO250
 300 GOSUB1000:GOSUB600:GOSUB500:GOSUB600
 310 GOSUB 1000:GOSUB600:GOTO300
 500 REM GUN MOVE
 510 POKE(KEY),254:K=PEEK(KEY)
 520 POKE(PG+GUN),32
 530 IF K=250 AND GUN>1 THEN GUN=GUN-1
 540 IFK=252 AND GUN<46 THEN GUN=GUN+1
 550 POKE(PG+GUN),29:IF A(0)>0THEN RETURN
 560 POKE(KEY),253:K=PEEK(KEY)
 570 IF K>247 THEN RETURN
 580 A(0)=GUN+1024:RETURN
 600 REM MISSILE MOVE
 610 IF A(0)=0THEN 700
 620 IFPEEK(PP+A(0))=149THENPOKE(PP+A(0)),32
 625 A(0)=A(0)-64
 630 IF A(0)<128 THEN A(0)=0:GOTO 700
 640 HH=PEEK(PP+A(0)):IFHH<>32ANDHH<>149THEN650
 645 POKE(A(0)+PP),149:GOTO 700
 650 IF HH>17 THEN 666
 660 FORHH=1TO100:POKE(PP+A(0)),HH:NEXT
 665 POKE(PP+A(0)),32:A(0)=0:GOTO700
 666 IF HH>100THENS=S+50
 667 IF HH<100THENS=S+10
 670 FORHH=1TO32:POKE(PP+A(0)-1),HH
 680 POKE(PP+A(0)),HH:POKE(PP+A(0)+1),HH:NEXT
 690 A(0)=0:GOSUB 950
 700 FORA1=1TO8:IFA(A1)=0THEN NEXT:RETURN
 705 A2=A(A1):A3=A2+PP:A4=PEEK(A3)
 710 IFA4=149THENPOKEA3,32
 720 A2=A2+64:IFA2>1088THEN805
 725 A3=A2+PP:A4=PEEK(A3)
 730 IFA4<>32THEN750
 740 POKE(A3),149:GOTO800
 750 FORHH=1TO32:POKEA3,HH:NEXT
 760 A2=0
 800 A(A1)=A2:NEXT:RETURN
 805 IFA4=29ORPEEK(A3-1)=29ORPEEK(A3+1)=29THENNN=1
 807 N=N-NN:IFNN=0THEN810
 808 POKE(53288+N*2),32:IFN<=0THEN900
 810 NN=0:FORHH=1TO32:POKEA3,HH
 820 POKEA3+1,HH:POKEA3-1,HH
 850 NEXT:GOTO760
 900 FORI=1TO16:PRINT:NEXT
 905 PRINT"END OF GAME":PRINT:PRINT"FINAL SCORE:";S
 910 END
 950 S$=STR$(S):LS=LEN(S$)
 960 FOR LL=1 TO LS
 970 POKE(53262+LL),ASC(MID$(S$,LL,1)):NEXT
 980 IFS/720=INT(S/720)THEN NI=-4:GOTO175
 990 RETURN
 1000 REM INVADER MOVE
 1010 NI=NI+4:IF NI>28 THEN 1100
 1020 FORII=0TO4:I1=PI+NI+II*64
 1030 I2=PEEK(I1):I3=PEEK(I1+1)
 1040 POKE(I1),32:POKE(I1+1),32
 1045 IFI3=149ORI2=149THENI3=32:I2=32
 1050 POKE(I1+GI),I2:POKE(I1+GI+1),I3
 1060 NEXT:IFPEEK(PI+NI+GI)=32OR A(NI/4+1)>0THEN RETURN
 1065 IF RND(1)>.5 THEN RETURN
 1070 A(NI/4+1)=PI-PP+NI+GI+2
 1080 POKE(PI+NI+GI+2),149:RETURN
 1100 NI=0:CI=CI+1:PI=PI+GI:IFCI>14THEN1200
 1110 GOTO1020
 1200 FORI=4TO0STEP-1:FORJ=0TO30STEP4
 1210 PX=PI+I*64+J:I1=PEEK(PX):I2=PEEK(PX+1)
 1215 POKE(PX),32:POKE(PX+1),32
 1220 POKE(PX+64),I1:POKE(PX+65),I2
 1230 NEXT J,I:GI=-GI:CI=0:PI=PI+64:NI=-4
 1240 IFPI>53965THEN900
 1250 RETURN
 2000 FORI=1TO16:PRINT:NEXT:RETURN
 9000 PRINT:PRINT"TO MOVE LEFT HOLD DOWN LEFT HAND SHIFT"
 9010 PRINT"TO MOVE RIGHT, HOLD DOWN RIGHT SHIFT"
 9020 PRINT"TO FIRE, HOLD DOWN '/' KEY UNTIL MISSILE APPEARS"
 9030 PRINT"YOU MAY MOVE AND FIRE AT THE SAME TIME"
 9040 POKE530,1:POKE57088,253
 9050 PRINT"PRESS SPACE BAR TO START";
 9060 WAIT 57088,255,255:GOTO 100
OK
