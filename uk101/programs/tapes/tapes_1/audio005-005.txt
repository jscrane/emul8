

 10 FORI=1TO16:PRINT:NEXT:PRINT"SPACE WAR!!!":PRINT"------------":PRINT
 20 INPUT"INSTRUCTIONS";Q$:IFLEFT$(Q$,1)<>"Y"THEN1000
 30 PRINT:PRINT"YOU ARE IN COMMAND OF A SPACE SHIP"
 40 PRINT"TRYING TO HIT THE ALIAN VESSLE (";CHR$(181);")"
 50 PRINT"AS OFTEN AS POSSIBLE WITH YOUR MISSILES"
 60 PRINT"TO ROTATE CLOCKWISE PRESS RIGHT SHIFT"
 70 PRINT"TO ROTATE ANTICLOCKWISE PRESS LEFT SHIFT"
 80 PRINT"TO BURN ROCKETS PRESS SPACE BAR"
 90 PRINT"TO LAUNCH A MISSILE PRESS 'CONTROL'":PRINT
 100 PRINT:PRINT"PRESS SPACE BAR TO START";
 110 POKE 530,1:POKE 57088,253:WAIT 57088,255,255
 1000 SC=53196:X=24:Y=8:N=236
 1001 FORI=1TO16:PRINT:NEXT:POKE54221,32
 1002 VX=0:VY=0:G=.04:X1=0:Y1=0:MT=0:H=0
 1003 V1=RND(1)*4-2:V2=RND(1)*2-1
 1005 KEY=57088:T=0:POKE 530,1
 1010 POKE(SC+INT(.5+X)+INT(.5+Y)*64),N
 1020 POKE(KEY),254:K=PEEK(KEY)
 1022 IF MT>30 AND K=190 THEN MT=0
 1023 P=P+1:IFP=10THENP=0
 1024 IFK<>250ANDK<>252THENP=9
 1025 IFP>0THEN1050
 1030 IFK=250 THEN N=N-1:IFN=235THEN N=239
 1040 IFK=252 THEN N=N+1:IFN=240THEN N=236
 1050 POKE(KEY),253:K=PEEK(KEY)
 1060 IF K<>239 THEN 1200
 1070 IFN=236 THEN VY=VY-G/2
 1080 IFN=237 THEN VX=VX+G
 1090 IFN=238 THEN VY=VY+G/2
 1100 IFN=239 THEN VX=VX-G
 1200 POKE(SC+INT(.5+X)+INT(.5+Y)*64),32
 1210 X=X+VX:Y=Y+VY
 1220 IFINT(X+.5)<1THENX=X+46
 1230 IFINT(X+.5)>46THENX=X-46
 1240 IFINT(.5+Y)<1THENY=Y+16
 1250 IFINT(.5+Y)>16THENY=Y-16
 1260 POKE(SC+INT(.5+X)+INT(.5+Y)*64),N
 1265 GOSUB 1300
 1270 POKE(SC+INT(.5+X1)+INT(.5+Y1)*64),32
 1280 X1=X1+V1:Y1=Y1+V2
 1282 IFINT(.5+X1)<1THENX1=X1+46
 1284 IFINT(.5+X1)>46THENX1=X1-46
 1286 IFINT(.5+Y1)<1THENY1=Y1+16
 1288 IFINT(.5+Y1)>16THENY1=Y1-16
 1290 POKE(SC+INT(.5+X1)+INT(.5+Y1)*64),181
 1295 GOSUB1300:T=T+1:MT=MT+1:IFT>1000THEN2000
 1297 GOTO 1020
 1300 PM=SC+INT(.5+MX)+INT(.5+MY)*64
 1302 IF MT>20 THEN POKE(PM),32:RETURN
 1303 IF (X1-MX)^2+(Y1-MY)^2>4 THEN 1308
 1304 H=H+1:V1=RND(1)*4-2:V2=RND(1)*2-1
 1305 FORI=0TO255:POKE(PM),I:NEXT:MT=100
 1306 POKE(SC+INT(.5+X1)+INT(.5+Y1)*64),32
 1307 X1=X1+RND(1)*20-10:Y1=Y1+RND(1)*10-5
 1308 POKE(PM),32
 1320 IF MT>1 THEN 1400
 1330 DM=N-235:M1=VX/2:M2=VY/2
 1340 IF DM=1 OR DM=3 THEN M2=M2+(DM-2)/2
 1350 IF DM=2 OR DM=4 THEN M1=M1-(DM-3)
 1360 MX=X:MY=Y:NM=DM*2+14
 1400 MX=MX+M1:MY=MY+M2
 1410 IFINT(.5+MX)<1THENMX=MX+46
 1420 IFINT(.5+MX)>46THENMX=MX-46
 1430 IFINT(.5+MY)<1THENMY=MY+16
 1440 IFINT(.5+MY)>16THENMY=MY-16
 1450 POKE(SC+INT(.5+MX)+INT(.5+MY)*64),NM
 1460 RETURN
 1500 PRINT:INPUT"ANOTHER TRY";Q$
 1510 IFLEFT$(Q$,1)="Y"THEN1000
 1520 PRINT:PRINT"THANKYOU":PRINT:END
 2000 FORI=1TO16:PRINT:NEXT:PRINT"END OF GAME"
 2010 PRINT:PRINT"YOU SCORED:";H;"HITS":PRINT:GOTO1500
 2320 IF MT>0 THEN 1400
OK
