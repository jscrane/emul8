

 5 POKE11,0:POKE12,253
 10 FORI=1TO16:PRINT:NEXT
 20 PRINT"KING!!!":PRINT"-------":PRINT
 30 INPUT"INSTRUCTIONS";Q$:IFLEFT$(Q$,1)<>"Y"THEN1000
 40 PRINT:PRINT" YOU HAVE BEEN CHOSEN TO BE RULER OF A SMALL"
 50 PRINT"PACIFIC ISLAND FOR A TERM OF EIGHT YEARS."
 60 PRINT"THE ISLAND HAS 1000 SQ. MILES OF FOREST"
 70 PRINT"AND 1000 SQ. MILES OF FARM!LAND."
 80 PRINT"THE LOCAL CURRENCY IS 'POBBLE BEADS', EACH"
 90 PRINT"COUNTRYMAN NEEDS 100PB TO STAY ALIVE"
 100 PRINT"YOU MAY PLANT FARM LAND OR SELL IT TO INDUSTRY"
 110 PRINT"(INDUSTRY WILL NOT BUY FOREST LAND)"
 120 PRINT"YOU MUST MANAGE THE ECONOMY FOR YOUR"
 130 PRINT"EIGHT YEAR TERM (AND YOU MAY FILCH MONEY"
 140 PRINT"FROM THE TREASUREY EACH YEAR - BUT DONT"
 150 PRINT"GET CAUGHT!)":PRINT"GOOD LUCK!"
 200 PRINT:PRINT"(PRESS SPACE TO START)";
 210 X=USR(0)
 1000 CLEAR:TR=INT(60000-(RND(1)-RND(1))*1000)
 1010 FL=2000:C=INT(500+(RND(1)-RND(1))*10)
 1100 P1=INT(95+RND(1)*10):C1=INT(RND(1)*5+10)
 1101 FORI=1TO16:PRINT:NEXT
 1107 PRINT"YEAR";TM+1;":  ";
 1110 PRINT"YOU HAVE";TR;"PB IN THE TREASURY"
 1120 PRINTC;"COUNTRYMEN":IFW>0THENPRINTW;"FOREIGN WORKERS"
 1130 PRINTFL;"SQ. MILES OF LAND."
 1140 PRINT"THIS YEAR IT COSTS";C1;"PB TO PLANT 1 SQ. MILE"
 1150 PRINT"INDUSTRY WILL BUY LAND FOR";P1;"PB PER SQ. MILE"
 1200 INPUT"HOW MANY SQ MILES TO SELL TO INDUSTRY";S:IFS<0THEN1200
 1210 IFS<FL-1000THEN1230
 1220 PRINT"YOU ONLY HAVE";FL-1000;"SQ. MILES OF FARM LAND":GOTO1200
 1230 FL=FL-S:IL=IL+S:TR=TR+S*P1
 1240 PRINT"HOW MUCH WILL YOU DISTRIBUTE"
 1245 INPUT"TO YOUR COUNTRYMEN";D
 1250 IFD<0THEN1240
 1260 IFD>TRTHENGOSUB9999:GOTO1240
 1265 TR=TR-D
 1270 INPUT"HOW MANY SQ. MILES TO PLANT";PL:IFPL<0THEN1270
 1271 IFPL*C1>TRTHENGOSUB9999:GOTO1270
 1272 IFPL>C*2THENPRINT"A PERSON CAN ONLY PLANT 2 SQ. MILES":GOTO1270
 1273 IFPL<=FL-1000THEN1280
 1274 PRINT"YOU ONLY HAVE";FL-1000;"SQ. MILES OF FARM LAND":GOTO1270
 1280 TR=TR-PL*C1:IFIL=0THEN1300
 1290 INPUT"HOW MUCH TO SPEND ON POLLUTION CONTROL";PC:IFPC<0THEN1290
 1295 IFPC>TRTHENGOSUB9999:GOTO1290
 1297 TR=TR-PC
 1300 INPUT"HOW MUCH TO FILCH(TO SWISS BANK ACCOUNT)";FI:IFFI<0THEN1300
 1305 FORI=1TO16:PRINT:NEXT
 1310 IFFI>TRTHENGOSUB9999:GOTO1300
 1315 IFFI=0THEN1400
 1320 TR=TR-FI:SB=SB+FI:IF(2*FI/TR)<RND(1)THEN1400
 1330 PRINT:PRINT"YOU ARE CAUGHT FILCHING THE TREASUREY"
 1340 PRINT"AND ARE EXECUTED BY THE MOB!":PRINT:PRINT:END
 1400 NC=INT(D/100):IFNC>=CTHEN1500
 1410 PRINTC-NC;"OF THE POPULATION DIED OF STARVATION!"
 1420 DD=C-NC:IFDD>200THENPRINTDD;"PEOPLE";:GOTO9000
 1430 IFNC/C<2/3THENPRINT"OVER ONE THIRD OF THE POPULATION":GOTO9000
 1440 IFDD<5ORTR<100THEN1460
 1445 PRINT"PEOPLE HAVE STARVED WHILE THERE IS MONEY"
 1450 PRINT"IN THE TREASURY":GOTO9010
 1460 C=C-DD:GOSUB8000
 1500 W1=ABS(INT(S+RND(1)*10-RND(1)*20))
 1510 IFS*W1=0THEN1600
 1520 PRINTW1;"FOREIGN WORKERS CAME TO THE ISLAND":W=W+W1
 1600 F1=INT(RND(1)*IL):IFPC>25THENF1=INT(F1*25/PC)
 1601 IFF1=0THEN1610
 1605 PRINTF1;"PEOPLE DIED OF CARBON MONOXIDE POISONING"
 1609 DD=F1:GOSUB8000:C=C-F1
 1610 PD=INT((NC-C)/10+PC/25-(IL/50-F1/2))
 1620 PRINTABS(PD);"PEOPLE ";:IFPD>=0THENPRINT"CAME TO";
 1630 IFPD<0THENPRINT"LEFT";
 1640 PRINT" THE ISLAND":C=C+PD
 1650 PRINT"OUT OF";PL;"SQ. MILES PLANTED";
 1655 FA=INT(IL*(RND(1)+1.5)/2):IFPC>1000THENFA=INT(FA*1000/PC)
 1660 HA=PL-FA:IFHA<0THENHA=0
 1665 IFRND(1)*8>TMORDR=1THEN1670
 1667 HA=INT(HA*RND(1)/2):DR=2
 1670 PRINTHA;"HARVESTED"
 1675 IFDR=2THENPRINT"   DUE TO A DROUGHT!!!":DR=1
 1680 PR=INT(HA*P1/2):PRINT"MAKING";PR;"PB PROFIT"
 1690 TR=TR+PR:IFHA<PLTHENPRINT"LOSS DUE TO AIR AND WATER POLLUTION"
 1700 OT=TT:TT=INT(C*22+RND(1)*500-IL*15):IFTT<0THENTT=0
 1710 PRINT"TOURIST TRADE MAKES";TT;"PB"
 1720 IF(OT>TT)ORIL=0THEN1740
 1730 PRINT"LESS DUE TO POLLUTION"
 1740 IFW<CTHEN1800
 1750 PRINT"FOREIGN WORKERS EXCEED NATIVE POPULATION"
 1760 PRINT"AND REVOLT. YOU ARE DEPOSED AND"
 1770 IFRND(1)>.5THEN1790
 1780 PRINT"ARE NOW LANGUISHING IN PRISON":END
 1790 PRINT"ARE EXECUTED":END
 1800 TR=TR+TT:TM=TM+1:IFTM=8THEN2000
 1805 PRINT"(PRESS SPACE TO CONTINUE)";
 1810 X=USR(0):GOTO1100
 2000 PRINT:PRINT"YOU HAVE SUCCESFULLY SURVIVED YOUR"
 2010 PRINT"8 YEAR TERM LEAVING";TR;"PB IN THE TREASUREY"
 2020 PRINT"BUT TAKING";SB;"PB IN A PRIVATE SWISS"
 2030 PRINT"BANK ACCOUNT ";:IFSB<11THENPRINT"(BACK ON THE DOLE!)":END
 2040 IFSB<101THENPRINT"(TO LAST YOU TILL NEXTYEAR!)":END
 2050 IFSB<501THENPRINT"(DONT SPEND IT ALL AT ONCE!)":END
 2060 PRINT"(TO KEEP YOU IN THE LUXURY"
 2070 PRINT"YOU ARE ACCUSTOMED TO!)":END
 8000 FE=DD*9:PRINT"YOU HAD TO SPEND";FE;"PB";
 8010 PRINT" FUNERAL EXPENSES":TR=TR-FE:IFTR>0THENRETURN
 8020 PRINT"INSUFFICIENT RESERVES...LAND WAS SOLD"
 8030 SO=INT(1-TR/P1):IL=IL+SO:FL=FL-SO:TR=TR+SO*P1:RETURN
 9000 PRINT" DIED IN ONE YEAR!!"
 9010 PRINT"DUE TO THIS EXTREME MISMANAGEMENT, NOT ONLY"
 9020 PRINT"HAVE YOU BEEN IMPEACHED AND THROWN OUT"
 9030 PRINT"OF OFFICE, BUT YOU HAVE ALSO"
 9040 ONINT(RND(1)*3)+1GOTO9050,9060,9070
 9050 PRINT"BEEN DEPORTED":END
 9060 PRINT"BEEN EXECUTED BY THE REVOLUTIONARY COURT":END
 9070 PRINT"BEEN THROWN IN PRISON":END
 9998 END
 9999 PRINT"THINK AGAIN!"
 10000 PRINT"YOU ONLY HAVE";TR;"PB IN THE TREASURY":RETURN
OK
