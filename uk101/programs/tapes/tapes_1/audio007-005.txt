

 1 POKE 11,0:POKE 12,253
 10 FORI=1TO16:PRINT:NEXT:PRINT"        LIFE!!!
 20 PRINT"        -------":PRINT:PRINT
 30 INPUT"DO YOU KNOW THE RULES";Q$:IFLEFT$(Q$,1)="N"THEN1000
 40 FORI=1TO16:PRINT:NEXT
 45 DIMN(1155),C(250)
 50 L=32:LL=53248:POKE(54221),32:P=54221
 60 X=USR(0):X=PEEK(531)
 62 IFX=32THENPOKE(P),32:GOTO200
 65 IF X>52THEN150
 67 IF PEEK(P)=161THENPOKE(P),L
 70 IFX=49THEN P=P-1
 80 IFX=50THEN P=P+1
 90 IFX=51THEN P=P-64
 100 IFX=52THEN P=P+64
 105 IF X=28 THEN 250
 110 L=PEEK(P):POKE(P),161:GOTO 60
 150 POKE(P),42
 200 GOTO 60
 250 REM START LIFE
 260 PP=53248:GN=0:NC=0
 270 FORI=0TO1024
 280 IFPEEK(I+PP)=42THENC(NC)=I:NC=NC+1
 290 NEXT
 300 PRINTCHR$(13);TAB(47)CHR$(13);:POKE14,0
 310 PRINT" GENERATION:";GN,"CELLS:";NC;
 315 FORI=0TO1024:N(I)=0:NEXT
 317 IFNC=0THEN390
 320 FORI=0TONC-1:C=C(I)+65
 360 N(C)=N(C)+10:N(C-65)=N(C-65)+1:N(C-64)=N(C-64)+1:N(C-63)=N(C-63)+1
 370 N(C-1)=N(C-1)+1:N(C+1)=N(C+1)+1:N(C+63)=N(C+63)+1
 380 N(C+64)=N(C+64)+1:N(C+65)=N(C+65)+1:NEXT
 390 NC=0:FORI=0TO1024:N=N(I+65)
 400 IFN=3ORN=12ORN=13THENC(NC)=I:NC=NC+1
 410 IFN=3THENPOKEI+PP,42
 420 IFN=10ORN=11ORN>13THENPOKEI+PP,32
 430 NEXT
 450 IFPEEK(57088)<>254THENX=47:GOTO70
 500 GN=GN+1:GOTO300
 1000 FORI=1TO10:PRINT:NEXT
 1005 PRINT"THIS PROGRAM SIMULATES A COLINY OF"
 1010 PRINT"LIVING ORGANISMS. THE RULES FOR SURVIVAL"
 1020 PRINT"OF ANY PARTICULAR CELL ARE:-"
 1030 PRINT"1 A CELL SURVIVES IF THERE ARE TWO OR THREE"
 1040 PRINT"  CELLS IMMIDIATELY SURROUNDING IT"
 1050 PRINT"2 A CELL DIES (OF LONLINESS) IF THERE IS"
 1060 PRINT"  LESS THAN TWO NEIGHBOURS, IT DIES
 1070 PRINT"  (OF OVERCROWDING) IF THERE ARE MORE THAN 3"
 1080 PRINT"3 A CELL IS BORN WHEN AN EMPTY SPACE"
 1090 PRINT"  HAS THREE NEIGHBOURS."
 1100 PRINT"TO SET UP THE SCREEN PRESS:-"
 1110 PRINT"1=LEFT  2=RIGHT   3=UP    4=DOWN"
 1130 PRINT"PRESS ANY KEY TO PLANT A CELL"
 1140 PRINT"PRESS 'RUB OUT' TO START LIFE"
 1150 PRINT"TO STOP, HOLD DOWN SHIFT UNTIL CURSER APPEARS"
 1160 POKE 530,1:POKE 57088,253
 1170 PRINT"(PRESS SPACE TO START)";:WAIT 57088,255,255
 1180 POKE 530,0:GOTO 40
OK
